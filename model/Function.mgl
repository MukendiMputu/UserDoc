//import "model/UserDoc.mgl" as docMgl
import "platform:/resource/info.scce.cinco.product.userdoc/src-gen/info/scce/cinco/product/userdoc/mglid/userdoc/impl/userdoc.ecore" as UserDocTool
id info.scce.cinco.product.function.mglid
stylePath "model/Function.style"

@primeviewer
@generatable("info.scce.cinco.product.userdoc.codegen.GenerateFunction", "/src/")
graphModel FunctionGraphModel {
	iconPath "icons/UserDocFunction.png"
	diagramExtension "func"
	containableElements(*)
	attr EString as name
}

@icon("icons/Start.png")
node StartNode {
	style StartNode
	outgoingEdges (*[1,1])
}

@icon("icons/End.png")
node EndNode{
	style EndNode
	incomingEdges ({Edge,LabeledEdge}[1,*])
}

@icon("icons/Container.png")
container FeatureContainer {
	style grayContainer("${title}")
	containableElements(*)
	attr EString as title
	incomingEdges (*[1,*])
	outgoingEdges ({Edge,LabeledEdge}[1,*])
}

node DocNode{
	style docNode("${mgl.modelName}")
	prime UserDocTool.UserDocGraphModel as mgl
	//prime docMgl::UserDocGraphModel as mgl
	incomingEdges (*[1,*])
	outgoingEdges ({Edge,LabeledEdge}[1,*])
}

node CommentText {
	style commentStyle("${content}")
	attr EString as content
	attr FeatureContainer as referenceTo
	outgoingEdges (CommentEdge[1,*])
}

edge Edge {
	style simpleArrow
}

edge LabeledEdge {
	style labeledArrow("${label}")
	attr EString as label
}

edge CommentEdge {
	style triangleArrow
}